plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

// Read versions from gradle.properties
def compileSdkVer = project.ANDROID_COMPILE_SDK.toInteger()
def minSdkVer = project.ANDROID_MIN_SDK.toInteger()
def targetSdkVer = project.ANDROID_TARGET_SDK.toInteger()
def javaVer = JavaVersion.toVersion(project.JAVA_VERSION)

android {
    namespace "dev.mgks.swv.sample"
    compileSdk compileSdkVer

    defaultConfig {
        applicationId "dev.mgks.swv.sample"
        minSdk minSdkVer
        targetSdk targetSdkVer
        versionCode 1
        versionName "1.0"
    }

    compileOptions {
        sourceCompatibility javaVer
        targetCompatibility javaVer
    }
    kotlinOptions {
        jvmTarget = javaVer.toString()
    }
}

dependencies {
    implementation project(':library')
    implementation 'androidx.core:core-ktx:1.17.0'
    implementation 'androidx.appcompat:appcompat:1.7.1'
}

// CONDITIONAL FIREBASE CONFIGURATION
// Only apply the plugin if the secret json file exists.
// This allows JitPack to build the library without crashing on the sample app.
if (file('google-services.json').exists()) {
    apply plugin: 'com.google.gms.google-services'
} else {
    println "swv-fcm-sync: google-services.json not found. Skipping Firebase Configuration."
}